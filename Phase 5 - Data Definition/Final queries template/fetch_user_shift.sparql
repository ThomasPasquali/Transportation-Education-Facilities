SELECT ?user ?shift ?arrive_before ?leave_after
WHERE {
    ## Parameters
    BIND ("2023-10-31T07:00:00"^^xsd:dateTime as ?curr_datetime)
    BIND ("Mario" as ?username)
    
    ## Select only one user
    ?user prop:has_end_user_name_GID-16021 ?username.

    ## Compute weekday
    BIND (ofn:daysBetween(?curr_datetime, "1967-01-01T10:00:00+01:00"^^xsd:dateTime) AS ?days)
    BIND (floor(?days - (7 * floor(?days / 7))) AS ?curr_weekday)

    ## Join user shifts, schedules and related data properties
    ?shift prop:involvement_GID-16019 ?user.
	 OPTIONAL { ?shift prop:occurence_schedule_GID-16046 ?ws.
                ?ws prop:has_weekly_schedule_monday_GID-80758 ?monday;
                    prop:has_weekly_schedule_tuesday_GID-80759 ?tuesday;
                    prop:has_weekly_schedule_wednesday_GID-80760 ?wednesday;
                    prop:has_weekly_schedule_thursday_GID-80761 ?thursday;
                    prop:has_weekly_schedule_friday_GID-80762 ?friday;
                    prop:has_weekly_schedule_saturday_GID-80763 ?saturday;
                    prop:has_weekly_schedule_sunday_GID-80757 ?sunday.
    }.
    OPTIONAL { 	?shift prop:occurence_schedule_exception_GID-16047 ?ex. 
                ?ex prop:has_schedule_exception_date_GID-16027 ?date_ex;
                    prop:has_schedule_exception_type_GID-31834 ?type_ex.
    }.
    OPTIONAL { ?shift prop:has_shift_arrive_before_GID-16022 ?arrive_before }.
    OPTIONAL { ?shift prop:has_shift_leave_after_GID-16023 ?leave_after }.

    ## Compute time difference between shift and current time
    BIND (ofn:minutesBetween(
    	xsd:dateTime(CONCAT(spif:dateFormat(?curr_datetime, "yyyy-MM-dd"), "T", IF(BOUND(?arrive_before),?arrive_before,?leave_after))),
        ?curr_datetime
    ) as ?time_diff) 
	
    ## Filter based on schedule exception (higher priority) or weekly schedule
    # TODO handle exception ?type_ex = "0"
    FILTER (IF(BOUND(?ex),
            ## Exception is present
            ?type_ex = "1" && ?date_ex = spif:dateFormat(?curr_datetime, "yyyyMMdd") && !BOUND(?ws),
            ## Exception is NOT present
            (?curr_weekday = "0"^^xsd:decimal && ?monday = "1") || 
            (?curr_weekday = "1"^^xsd:decimal && ?tuesday = "1") || 
            (?curr_weekday = "2"^^xsd:decimal && ?wednesday = "1") || 
            (?curr_weekday = "3"^^xsd:decimal && ?thursday = "1") || 
            (?curr_weekday = "4"^^xsd:decimal && ?friday = "1") || 
            (?curr_weekday = "5"^^xsd:decimal && ?saturday = "1") || 
            (?curr_weekday = "6"^^xsd:decimal && ?sunday = "1")
        )
    )
}
ORDER BY DESC(?ex) ?time_diff
LIMIT 1